# The importApply argument. Use this to reference things defined locally,
# as opposed to the flake where this is imported.
localFlake:

# Regular module arguments; self, inputs, etc all reference the final user flake,
# where this module was imported.
_: {
  perSystem =
    {
      config,
      # self',
      # inputs',
      pkgs,
      # system,
      ...
    }:
    {
      devenv.shells.default = {
        packages =
          let
            mdformatAndPlugins = with pkgs.python312Packages; [
              mdformat
              mdformat-frontmatter
              mdformat-tables
            ];
          in
          with pkgs;
          [
            hugo
            markdownlint-cli
            zenn-cli
            pnpm
          ]
          ++ mdformatAndPlugins;
        processes = {
          hugo-server = {
            exec = ''
              cd $REPO_ROOT/hugo-blog
              hugo server --buildFuture --buildDrafts
            '';
          };
          zenn-server = {
            exec = ''
              cd $REPO_ROOT
              zenn preview
            '';
          };
        };
        scripts = {
          list =
            let
              inherit (pkgs) lib;
            in
            {
              exec = ''
                echo
                echo ðŸ¦¾ Helper scripts you can run to make your development richer:
                echo ðŸ¦¾
                ${pkgs.gnused}/bin/sed -e 's| |â€¢â€¢|g' -e 's|=| |' <<EOF \
                | ${pkgs.util-linuxMinimal}/bin/column -t | ${pkgs.gnused}/bin/sed -e 's|^|ðŸ¦¾ |' -e 's|â€¢â€¢| |g'
                ${lib.generators.toKeyValue { } (
                  lib.mapAttrs (name: value: value.description) config.devenv.shells.default.scripts
                )}
                EOF
                echo
              '';
              description = "devenvã§å®šç¾©ã—ãŸã®scriptsä¸€è¦§";
            };
          hugo-new-content = {
            exec = ''
              cd $REPO_ROOT/hugo-blog
              hugo new content content/post/$1/index.md
            '';
            description = "hugo-blogåŸ·ç­†ç”¨: æ–°è¦ã®è¨˜äº‹ä½œæˆ";
          };
        };
        enterShell = ''
          hugo version
          echo zenn v$(zenn --version)
          list
        '';
      };
    };
}
